class Controller

values

	private open  : real = ValveActuator`open;
	private close : real = ValveActuator`close;

instance variables

	max_level : real;
	min_level : real;

	sensor   : LevelSensor;
  
	valve : ValveActuator;

operations

	public Controller : real * real * LevelSensor * ValveActuator ==> Controller
	Controller(max,min,s,v) == (
		max_level := max;
		min_level := min;
		sensor	  := s;
		valve	  := v;
	);

	private control_loop : () ==> ()
	control_loop() == cycles(2) (
	   
	    -- retrieve the first value from Co-SIM
	    dcl current_level : real := sensor.getLevel();
				
	    -- determine feedtank valve setting
	    if current_level >= max_level
	    then valve.setValveState(open)
	    else if current_level <= min_level
	    then valve.setValveState(close);  
   );

thread

	periodic(10E6,0,0,0)(control_loop);	 
		 
end Controller
